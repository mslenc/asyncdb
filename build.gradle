plugins {
    id "java-library"
    id "maven-publish"
    id "com.github.michaelruocco.embedded-mysql-plugin" version "2.1.11"
    id "signing"
}

group "io.github.mslenc"
version "1.6.3"

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    // https://mvnrepository.com/artifact/io.netty/netty-all
    api 'io.netty:netty-all:4.1.112.Final'

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    implementation "org.slf4j:slf4j-api:1.7.36"

    testImplementation "junit:junit:4.13.2"

    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    testImplementation "ch.qos.logback:logback-classic:1.3.14"

    // https://mvnrepository.com/artifact/org.postgresql/postgresql
    testImplementation "org.postgresql:postgresql:42.7.2"
}

embeddedMysql {
    url = 'jdbc:mysql://127.0.0.1:3356/databaseName'
    username = 'asyncdb'
    password = 'asyncdb'
    version = 'v5_7_latest'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'asyncdb'
            from components.java

            pom {
                name = "asyncdb"
                description = "An asynchronous driver for MySQL (and API wrapper around JDBC)."
                url = "https://github.com/mslenc/asyncdb"

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'mslenc'
                        name = 'Mitja Slenc'
                        email = 'mslenc@gmail.com'
                    }
                }

                scm {
                    connection = "scm:git:https://github.com/mslenc/asyncdb.git"
                    developerConnection = "scm:git:git@github.com:mslenc/asyncdb.git"
                    url = "https://github.com/mslenc/asyncdb"
                }
            }
        }
    }

    repositories {
        maven {
            url = "s3://mslenc-releases/maven2"
            credentials(AwsCredentials) {
                accessKey = awsAccessKeyMslenc
                secretKey = awsSecretKeyMslenc
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

test {
  minHeapSize = "8192m" // initial heap size
  maxHeapSize = "8192m" // maximum heap size
  jvmArgs '-XX:MaxPermSize=256m' // mem argument for the test JVM
}
